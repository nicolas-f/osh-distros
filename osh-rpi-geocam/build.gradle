apply from: gradle.oshCoreDir + '/common.gradle'

allprojects  {
  version = oshCoreVersion
  
  configurations.all {
    resolutionStrategy {
      dependencySubstitution.all { DependencySubstitution dependency ->
        if (dependency.requested instanceof ModuleComponentSelector) {
          if (dependency.requested.group == 'org.sensorhub') {
            dependency.useTarget findProject(':'+dependency.requested.module)
          }
        }
      }
    }
  }
}

dependencies {
  project.subprojects.each {
    p -> compile p
  }
}

distributions {
  main {
    contents {
      from 'src/scripts'
      from 'src/config'
      from 'README.md'
      into('web') {
        from 'web'
      }
      into('lib') {
        from project.configurations.compileClasspath
      }
    }
  }
}





